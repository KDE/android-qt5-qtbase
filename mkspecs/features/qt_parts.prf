load(qt_build_config)

TEMPLATE = subdirs

sub_src.subdir = src
sub_src.target = sub-src
SUBDIRS = sub_src

exists($$_PRO_FILE_PWD_/tools/tools.pro) {
    sub_tools.subdir = tools
    sub_tools.target = sub-tools
    sub_tools.depends = sub_src
    !contains(QT_BUILD_PARTS, tools): sub_tools.CONFIG = no_default_target no_default_install
    SUBDIRS += sub_tools
}

exists($$_PRO_FILE_PWD_/examples/examples.pro):contains(QT_BUILD_PARTS, examples){
    sub_examples.subdir = examples
    sub_examples.target = sub-examples
    sub_examples.depends = sub_src
    SUBDIRS += sub_examples
}

# Some modules still have these
exists($$_PRO_FILE_PWD_/demos/demos.pro):contains(QT_BUILD_PARTS, examples) {
    sub_demos.subdir = demos
    sub_demos.target = sub-demos
    sub_demos.depends = sub_src
    SUBDIRS += sub_demos
}

exists($$_PRO_FILE_PWD_/tests/tests.pro):contains(QT_BUILD_PARTS, tests) {
    sub_tests.subdir = tests
    sub_tests.target = sub-tests
    sub_tests.depends = sub_src   # The tests may have a run-time only dependency on other parts
    sub_tests.CONFIG = no_default_install
    sub_tools.CONFIG -= no_default_target
    sub_examples.CONFIG -= no_default_target
    sub_demos.CONFIG -= no_default_target
    SUBDIRS += sub_tests
}

QT_BUILD_PARTS -= libs tools examples tests
!isEmpty(QT_BUILD_PARTS): warning("Unknown build part(s): $$QT_BUILD_PARTS")
